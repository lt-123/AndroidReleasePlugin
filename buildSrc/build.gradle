plugins {
    id "groovy"
    // maven 使用 uploadArchives 发布到本地
    id "maven"
    id 'maven-publish'
    // 发布到 gradle plugin 仓库的两个插件
    id "com.gradle.plugin-publish" version "0.12.0"
    id "java-gradle-plugin"
}

// 插件配置
ext {
    pluginId = 'xyz.liut.apk-release'
    pluginImplementationClass = 'xyz.liut.releaseplugin.ReleasePlugin'
    pluginVersion = '1.0.' + getVersion() + '-beta'
    pluginGroup = "com.github.lt-123"
    pluginArtifactId = "apk-release"
}

repositories {
    google()
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.tools.build:gradle:4.1.1'
    implementation 'xyz.liut.logcat:logcat:0.1.6'
}

gradlePlugin {
    plugins {
        releasePlugin {
            id = pluginId
            implementationClass = pluginImplementationClass
        }
    }
}

pluginBundle {
    website = 'https://github.com/lt-123/AndroidReleasePlugin'
    vcsUrl = 'https://github.com/lt-123/AndroidReleasePlugin'
    description = 'Android release plugin'
    tags = ['android', 'release']

    plugins {
        releasePlugin {
            displayName = 'apk-release'
            description = 'Android release plugin'
            tags = ['android', 'release']
            version = pluginVersion
        }
    }

    mavenCoordinates {
        groupId = pluginGroup
        artifactId = pluginArtifactId
        version = pluginVersion
    }
}


static int getVersion() {
    def count = "git rev-list HEAD --count".execute().text.trim()
    return count.toInteger()
}
