plugins {
    id "groovy"
    // 使用 uploadArchives 发布到本地
    id 'maven-publish'
    // 发布到 gradle plugin 仓库的两个插件
    id "com.gradle.plugin-publish" version "0.12.0"
    id "java-gradle-plugin"
    // 发布到 jcenter
    id "com.jfrog.bintray" version "1.8.5"
}

// 插件配置
ext {
    pluginId = 'xyz.liut.apk-release'
    pluginImplementationClass = 'xyz.liut.releaseplugin.ReleasePlugin'
    pluginVersion = '1.0.' + getVersion() + '-beta'
//    pluginGroup = "com.github.lt-123"
//    pluginGroup = "xyz.liut.gradleplugin"
    pluginGroup = "xyz.liut"
    pluginArtifactId = "apk-release"

    pluginVcsUrl = 'https://github.com/lt-123/AndroidReleasePlugin'
    pluginDesc = 'Android release plugin'
    pluginTags = ['android', 'release']

}

apply from: 'publish-gradle-m2.gradle'
apply from: 'publish-jcenter.gradle'

version = pluginVersion
group = pluginGroup

sourceCompatibility = "8"
targetCompatibility = "8"

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    google()
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.tools.build:gradle:4.1.1'
    implementation 'xyz.liut.logcat:logcat:0.1.6'
}

static int getVersion() {
    def count = "git rev-list HEAD --count".execute().text.trim()
    return count.toInteger()
}
