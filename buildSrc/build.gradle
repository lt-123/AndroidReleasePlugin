plugins {
    id "groovy"
    // maven 使用 uploadArchives 发布到本地
    id "maven"
    id 'maven-publish'
    // 发布到 gradle plugin 仓库的两个插件
    id "com.gradle.plugin-publish" version "0.12.0"
    id "java-gradle-plugin"
}

// 插件配置
ext {
    pluginId = 'xyz.liut.release'
    pluginImplementationClass = 'xyz.liut.releaseplugin.ReleasePlugin'
    pluginVersion = '1.0.' + getVersion() + '-alpha'
    pluginGroup = "xyz.liut.gradleplugin"
}

version = pluginVersion
group = pluginGroup

sourceCompatibility = "8"
targetCompatibility = "8"


repositories {
    google()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

//    testCompile 'junit:junit:4.13.1'

//    compile gradleApi()
//    compile localGroovy()

    // android plugin
    implementation 'com.android.tools.build:gradle:4.1.1'

//    compileOnly 'com.android.tools.build:gradle:4.1.1'
    implementation 'xyz.liut.logcat:logcat:0.1.6'
}

// 发布到本地
uploadArchives {
    repositories.mavenDeployer {
        // 仓库的路径，此处是项目根目录下的 repo 的文件夹
        repository(url: uri('../repo'))
        // groupId ，自行定义，一般是包名
        pom.groupId = group
        // artifactId ，自行定义
        pom.artifactId = pluginId
        // //version 版本号
        pom.version = version
    }
}

// Use java-gradle-plugin to generate plugin descriptors and specify plugin ids
gradlePlugin {
    plugins {
        releasePlugin {
            id = pluginId
            implementationClass = pluginImplementationClass
        }
    }
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
    website = 'https://www.liut.xyz'
    vcsUrl = 'https://github.com/lt-123/AndroidReleasePlugin'
    description = 'Android release plugin'
    tags = ['android', 'release']

    plugins {
        greetingsPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'ReleasePlugin'
        }
    }
}

static int getVersion() {
    def count = "git rev-list HEAD --count".execute().text.trim()
    return count.toInteger()
}
